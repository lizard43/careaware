<html>

<head>
<title>CareAware - Radiology Report</title>
</head>

<body background=speckle.gif bgcolor="#e5e5e5" style="MARGIN: 0px; PADDING-BOTTOM: 2px; PADDING-LEFT: 2px; PADDING-RIGHT: 2px; PADDING-TOP: 2px; WIDTH: 100%">

<A NAME="top"></A><!-- Start: ToolBar V2.0-->
<script language="JavaScript" src="toolbar.js"></script>
<script language="JavaScript" src="global.js"></script>

<script language="JavaScript"><!--//
var ToolBar_Supported = ToolBar_Supported;
if (ToolBar_Supported != null && ToolBar_Supported == true)
{
	setDefaultICPMenuColor("Darkblue", "Ghostwhite", "#FF3300");
	setToolbarBGColor("white");
	setICPBanner("careawar.jpg","","CareAware");
}
//--></script>
<script language="JavaScript" src="local.js"></script>
<script language="JavaScript"><!--//
if (ToolBar_Supported != null && ToolBar_Supported == true)
{
	drawToolbar();
}
//--></script>
<!-- End: ToolBar V2.0-->

<SCRIPT LANGUAGE=VBSCRIPT>

' this will cause an error if any variable is used without
'   being declared (DIM)
option explicit

' global Word application object.  Set in OpenDoc, destroyed in CloseDoc
dim appWord

'  Flag for detecting if we started Word or if it is already running
dim bStartedWord

'***************************************************************************
'
'  OnSave
'  This is called when the user clicks the SAVE button.
'
'***************************************************************************
sub OnSave

	' build Word document
	WriteDoc()
		
	' write info to a db
	WriteRadiologyTable()
	WritePatientTable()
		
	' clear form and get ready for next entry
	FormReset()
		
	msgbox "Information Saved..."
	
end sub

'***************************************************************************
'
'  OnLoad
'  This gets called automatically when the browser loads the page.
'  Setup from, fill dropdowns, etc.
'
'***************************************************************************
sub Window_onLoad()
	
	' get preferences from registry

	dim strPatientDBFolder

	DRegEdit1.hKey = 2  ' reHKEY_LOCAL_MACHINE	
	DRegEdit1.SubKeyPath = "Software\LLC\CareAware\RadiologyReport"
	
	' Patient db folder
	DRegEdit1.ValueType = 1  ' reStringType
	DRegEdit1.ValueName = "PatientDbFolder"
	DRegEdit1.ReadValue
	
    strPatientDBFolder = DRegEdit1.Value & "\" & "Radiology.mdb"

	' open physician db
	dim objConn
	set objConn = CreateObject("ADODB.Connection")
	objConn.Open "Driver={Microsoft Access Driver (*.mdb)};DBQ=" & strPatientDBFolder

	' build recordset for room number dropdown
	dim rsRoomNumber
	set rsRoomNumber = CreateObject("ADODB.Recordset")
	rsRoomNumber.ActiveConnection = objConn
	rsRoomNumber.Source = "Select * from PatientRoomNumbers"
	rsRoomNumber.Open
	
	' make sure that records exist
	if (not rsRoomNumber.EOF AND not rsRoomNumber.BOF) then
	
		rsRoomNumber.MoveFirst
		do until (rsRoomNumber.EOF)
		
			document.all.cbRoomNumber.AddItem rsRoomNumber("RoomNumber").Value
			
			rsRoomNumber.MoveNext
		loop
	end if
	rsRoomNumber.Close	
	set rsRoomNumber = nothing
	
	' build recordset for physician dropdown
	dim rsPhysician
	set rsPhysician = CreateObject("ADODB.Recordset")
	rsPhysician.ActiveConnection = objConn
	rsPhysician.Source = "Select * from Physicians order by lastname"
	rsPhysician.Open
	
	' make sure that records exist
	if (not rsPhysician.EOF AND not rsPhysician.BOF) then
	
		rsPhysician.MoveFirst		
		do until (rsPhysician.EOF)
		
			dim strName
			strName = rsPhysician("FirstName").Value + " " + _
				rsPhysician("LastName").Value + _
				"  " + rsPhysician("Title").Value
			document.all.cbPhysician.AddItem strName
			
			rsPhysician.MoveNext
		loop
	end if	
	rsPhysician.Close
	set rsPhysician = nothing

	' close db connection
	objConn.Close
	
	set objConn = nothing
	
	' build report filename
	
	dim strFileName
	dim dtDate
	dtDate = now
	
	' get preferences from registry
	DRegEdit1.hKey = 2  ' reHKEY_LOCAL_MACHINE	
	DRegEdit1.SubKeyPath = "Software\LLC\CareAware\RadiologyReport"
	
	' Report folder
	DRegEdit1.ValueType = 1  ' reStringType
	DRegEdit1.ValueName = "SaveReportFolder"
	DRegEdit1.ReadValue
	
    strFileName = DRegEdit1.Value & "\" & Month(dtDate) & Day(dtDate) & Year(dtDate) & "RS-rad.doc"
	txtFileName.value = strFileName
	
	' set focus to first field
	document.all.txtLastName.Focus()
		
end sub

'***************************************************************************
' 
'  WriteDoc
'  Open Word document and write fields
'
'***************************************************************************
sub WriteDoc

	' open Word doc.  Use the Filename text box because the user can edit the name
	OpenDoc(txtFileName.value)
	
	' write the report header
	WriteHeader()

	' write data

	dim i
	dim strLine

	strLine = "PATIENT NAME: "
	strLine = strLine + document.all.txtFirstName.Value + " " + document.all.txtLastName.Value

	' calc how many tabs are needed to get to half page	   
	i = 60 - len(strLine)
	do while (i > 0)
      strLine = strLine + chr(09)
      i = i - 6
   loop
   strLine = strLine + "MEDICAL RECORD NUMBER: "
   strLine = strLine + document.all.txtMRN.Value
   WriteLine(strLine)
   WriteLine("")

   strLine = "DATE OF BIRTH: "
   strLine = strLine & document.all.dtDOB.Value

	' calc how many tabs are needed to get to half page	   
   i = 60 - len(strLine)
   do while (i > 0)
      strLine = strLine + chr(09)
      i = i - 6
   loop
   strLine = strLine + "X-RAY NUMBER: "
   strLine = strLine & document.all.txtXRay.Value
   WriteLine(strLine)
   WriteLine("")

   strLine = "DATE OF EXAM: "
   strLine = strLine & document.all.dtDOE.Value

	' calc how many tabs are needed to get to half page	   
   i = 60 - len(strLine)
   do while (i > 0)
      strLine = strLine + chr(09)
      i = i - 6
   loop
   i = document.all.opStatus.SelectedIndex
   strLine = strLine & "PATIENT STATUS: "
   strLine = strLine & document.all.opStatus.Options(i).Text & " " & document.all.cbRoomNumber.Text
   WriteLine(strLine)
   WriteLine("")

   strLine = "ORDERING PHYSICIAN: "
   strLine = strLine & document.all.cbPhysician.Text
   WriteLine(strLine)
   WriteLine("")

   strLine = "EXAMINATION: "
   strLine = strLine & document.all.txtExamination.Value
   WriteLine(strLine)
   WriteLine("")

   strLine = "INDICATIONS: "
   strLine = strLine & document.all.txtIndications.Value
   WriteLine(strLine)

   WriteLine("_________________________________________________________________________________________________________")
   WriteLine("")
   WriteLine("")
   
   ' put a page break at end of report
   PageBreak
   
   ' close word doc
   CloseDoc(txtFileName.value)
	
end sub

'***************************************************************************
'
'  SetChangeFlag
'  This is called when edits are made to the edit boxes.
'
'***************************************************************************
sub SetChangeFlag
end sub

'***************************************************************************
'
'  OpenDoc
'  Opens an existing Word doc or creates a new one
'
'***************************************************************************
sub OpenDoc(strFileName)

	on error resume next

	' clear start flag
	bStartedWord = 0

	' open Word application

	' Use GetObject if an application is already running, if not then use CreateObject. 

'	Set appWord = GetObject(, "Word.Application")
	Set appWord = nothing

	' check if Word was already running
	If appWord is nothing Then

		Set appWord = CreateObject("Word.Application")

		' if still nothing, then something is wrong with Word
		If appWord is nothing Then
			MsgBox "MS Word is not properly installed."
			return
		End if

		' Don't display the application.
		appWord.Visible = false

		' shut off updating - it's much faster this way
		appWord.ScreenUpdating = FALSE

		' set flag
		bStartedWord = 1

	End if

	' check if document exists
	dim fso
	Set fso = CreateObject("Scripting.FileSystemObject")

	If (fso.FileExists(strFileName)) Then

		' Open a sample document.
		appWord.Documents.Open strFileName
						
	' doc doesn't exist, create it and setup the page	
	else
	
		' create new doc
		appWord.Documents.Add
				
		' save it
		appWord.ActiveDocument.SaveAs strFileName

		' set up margins
		AppWord.ActiveDocument.PageSetup.LineNumbering.Active = False
		          
		'wdOrientPortrait
		AppWord.ActiveDocument.PageSetup.Orientation = 0
		          
		' .5" = 36 pts
		AppWord.ActiveDocument.PageSetup.TopMargin = 36

		' .5"
		AppWord.ActiveDocument.PageSetup.BottomMargin = 36

		' .5"
		AppWord.ActiveDocument.PageSetup.LeftMargin = 36

		' .5"
		AppWord.ActiveDocument.PageSetup.RightMargin = 36

		' 0"
		AppWord.ActiveDocument.PageSetup.Gutter = 0

		' .5"
		AppWord.ActiveDocument.PageSetup.HeaderDistance = 36

		' .5"
		AppWord.ActiveDocument.PageSetup.FooterDistance = 36

		' 8.5" = 612 Pts
		AppWord.ActiveDocument.PageSetup.PageWidth = 612

		' 11" = 792 pts
		AppWord.ActiveDocument.PageSetup.PageHeight = 792
		          
		' wdSectionNewPage
		AppWord.ActiveDocument.PageSetup.SectionStart = 2
		          
		AppWord.ActiveDocument.PageSetup.OddAndEvenPagesHeaderFooter = False
		AppWord.ActiveDocument.PageSetup.DifferentFirstPageHeaderFooter = False
		          
		' wdAlignVerticalTop
		AppWord.ActiveDocument.PageSetup.VerticalAlignment = 0
		          
		AppWord.ActiveDocument.PageSetup.SuppressEndnotes = False
		AppWord.ActiveDocument.PageSetup.MirrorMargins = False
		
		' save again
		appWord.ActiveDocument.SaveAs strFileName
	end if
	
	' clear file system object
	set fso = nothing

end sub

'***************************************************************************
'
'  WriteLine
'  Inserts a line of text at the end of the document and carriage returns
'
'***************************************************************************
sub WriteLine(strLine)

	' goto end of doc
	appWord.Selection.EndKey 6  ' wdStory
	
	' set alignment to left wdAlignParagraphLeft
	appWord.Selection.ParagraphFormat.Alignment = 0
	
	' write text
	appWord.Selection.TypeText(strLine)
	
	' carriage return
	appWord.Selection.TypeParagraph
end sub

'***************************************************************************
'
'  WriteHeader
'  Write a centered header at the end of the doc (which should be at
'  the beginning of a new page.
'
'***************************************************************************
Sub WriteHeader

	' goto end of doc
	appWord.Selection.EndKey 6  ' wdStory
	
	' set style
	appWord.Selection.Style = appWord.ActiveDocument.Styles("title")
	
	' set font
    appWord.Selection.Font.Name = "Arial"
    appWord.Selection.Font.Size = 14
    
    ' center
    appWord.Selection.ParagraphFormat.Alignment = 1  ' wdAlignParagraphCenter
    
    ' text
    appWord.Selection.TypeText "MEDICAL CENTER"
    appWord.Selection.TypeParagraph
      
    ' set style
    appWord.Selection.Style = appWord.ActiveDocument.Styles("normal")
    
    ' set font
    appWord.Selection.Font.Name = "Arial Narrow"
    appWord.Selection.Font.Size = 11
    
    ' center
    appWord.Selection.ParagraphFormat.Alignment = 1 ' wdAlignParagraphCenter
      
    ' text
    appWord.Selection.TypeText "1000 NORTH AVE"
    appWord.Selection.TypeParagraph
    
    appWord.Selection.TypeText "VILLE, STATE 00000"
    appWord.Selection.TypeParagraph
    
    appWord.Selection.TypeText "(000)-000-0000"
    appWord.Selection.TypeParagraph
    
    appWord.Selection.TypeText ""
    appWord.Selection.TypeParagraph
    
    appWord.Selection.TypeText "RADIOLOGY REPORT"
    appWord.Selection.TypeParagraph
    appWord.Selection.TypeParagraph
    appWord.Selection.TypeParagraph
    appWord.Selection.TypeParagraph
   
End Sub

'***************************************************************************
'
'  PageBreak
'  Inserts a page break at end of the doc
'
'***************************************************************************
sub PageBreak
	' goto end of doc
	appWord.Selection.EndKey 6  ' wdStory
	
	' insert page break wdPageBreak
	appWord.Selection.InsertBreak 7
end sub

'***************************************************************************
'
'  CloseDoc
'  Closes document and quits Word
'
'***************************************************************************
sub CloseDoc(strFileName)

	' save
	appWord.ActiveDocument.SaveAs strFileName
	
	' close doc
	appWord.ActiveDocument.Close
	
	' close Word

	' if we started Word, then we want to shut it down
	if (bStartedWord = 1) then
		appWord.Quit   
	end if
	
	' clear Word object
	Set appWord = Nothing

end sub

'***************************************************************************
'
'  FormReset
'  Clears the form and reset focus to the first field
'
'***************************************************************************
sub FormReset

	' set focus to LAST NAME
	document.all.txtLastName.Focus()
	
	' reset all the items in the form
	document.forms(0).reset()
	
	' combo boxes (dropdowns) are activeX ctrls and need to be reset manually
	document.all.cbPhysician.Text = ""
	document.all.cbRoomNumber.Text = ""
end sub

'***************************************************************************
'
'  WriteRadiologyTable
'  Write a row into the RadiologyReport table
'
'***************************************************************************
sub WriteRadiologyTable
	
	' get preferences from registry

	dim strPatientDBFolder

	DRegEdit1.hKey = 2  ' reHKEY_LOCAL_MACHINE	
	DRegEdit1.SubKeyPath = "Software\LLC\CareAware\RadiologyReport"
	
	' Patient db folder
	DRegEdit1.ValueType = 1  ' reStringType
	DRegEdit1.ValueName = "PatientDbFolder"
	DRegEdit1.ReadValue

	' build path to the database	
    strPatientDBFolder = DRegEdit1.Value & "\" & "Radiology.mdb"
    
	' open physician db
	dim objConn
	set objConn = CreateObject("ADODB.Connection")
        objConn.Mode = 3 'adModeReadWrite
	objConn.Open "Driver={Microsoft Access Driver (*.mdb)};DBQ=" & strPatientDBFolder

	' build recordset
	dim rsPatient
	set rsPatient = CreateObject("ADODB.Recordset")
	rsPatient.ActiveConnection = objConn
	rsPatient.Source = "Select * from RadiologyReport where RecordNumber=0"
	rsPatient.LockType = 2
	rsPatient.CursorLocation = 3 'adUseClient
	rsPatient.CursorType = 2
	
	' open recordset
	rsPatient.Open

	' fill recordset with patient data
	dim iIndex
	rsPatient.AddNew
	rsPatient("MRN") = document.all.txtMRN.Value
	rsPatient("XRayNumber") = document.all.txtXRay.Value
	rsPatient("FirstName") = document.all.txtFirstName.Value
	rsPatient("LastName") = document.all.txtLastName.Value
	rsPatient("DOB") = document.all.dtDOB.Value
	rsPatient("DOE") = document.all.dtDOE.Value
	rsPatient("Physician") = document.all.cbPhysician.Text
	iIndex = document.all.opStatus.SelectedIndex
	rsPatient("Status") = document.all.opStatus.Options(iIndex).Text
	rsPatient("RoomNumber") = document.all.cbRoomNumber.Text
	rsPatient("Notes") = document.all.txtNotes.Value
	rsPatient("Filename") = document.all.txtFileName.Value
	rsPatient("Examination") = document.all.txtExamination.Value
	rsPatient("Indications") = document.all.txtIndications.Value
		
	' write the row
	rsPatient.Update
	
	' close it up and clear
	rsPatient.Close
	set rsPatient = nothing
	
	objConn.Close
	set objConn = nothing
	
end sub

'***************************************************************************
'
'  WritePatientTable
'  Write a row into the Patient table
'
'***************************************************************************
sub WritePatientTable
	
	' get preferences from registry

	dim strPatientDBFolder

	DRegEdit1.hKey = 2  ' reHKEY_LOCAL_MACHINE	
	DRegEdit1.SubKeyPath = "Software\LLC\CareAware\RadiologyReport"
	
	' Patient db folder
	DRegEdit1.ValueType = 1  ' reStringType
	DRegEdit1.ValueName = "PatientDbFolder"
	DRegEdit1.ReadValue
	
	' build path to the database	
    strPatientDBFolder = DRegEdit1.Value & "\" & "Radiology.mdb"
    
	' open physician db
	dim objConn
	set objConn = CreateObject("ADODB.Connection")
        objConn.Mode = 3 'adModeReadWrite
	objConn.Open "Driver={Microsoft Access Driver (*.mdb)};DBQ=" & strPatientDBFolder

	' build recordset
	dim rsPatient
	set rsPatient = CreateObject("ADODB.Recordset")
	rsPatient.ActiveConnection = objConn
	rsPatient.Source = "Select * from Patients where RecordNumber=0"
	rsPatient.LockType = 2
	rsPatient.CursorLocation = 3 'adUseClient
	rsPatient.CursorType = 2
	rsPatient.Open

	' fill recordset with patient data
	dim iIndex
	rsPatient.AddNew
	rsPatient("PatientID") = document.all.txtMRN.Value
	rsPatient("FirstName") = document.all.txtFirstName.Value
	rsPatient("LastName") = document.all.txtLastName.Value
	rsPatient("Birthdate") = document.all.dtDOB.Value

	' write the row
	rsPatient.Update

	' close it up and clear
	rsPatient.Close
	set rsPatient = nothing
	
	objConn.Close
	set objConn = nothing

end sub

</SCRIPT>

<form id=form1 name=form1>

  <TABLE align=center border=0 cellPadding=3 cellSpacing=1 width="100%" background="gray.gif" style="WIDTH: 100%">
    <TBODY>
    
    <TR>
      <TD align=left>
        <P align=left><FONT color=#0066cc size=6 
        face="Arial Black"><STRONG>&nbsp;Radiology 
      Report</FONT> </STRONG><FONT face="Arial Black"> 
  </FONT></P></TD>
      </TR></TBODY></TABLE>

<p align="center"><font color="#000000" size="3"
 face="Arial Narrow"></font></p>

<table border="0" CELLSPACING=15 CELLPADDING=%3 WIDTH="100%" style="HEIGHT: 435px; POSITION: relative; TOP: 3%">
    <tr>
        <td><STRONG><FONT face="MS Sans Serif" 
     size=2 
     >Patient Last Name: 
      </FONT></STRONG></td>
        <td><INPUT ID=txtLastName SIZE=30 MAXLENGTH=80 onchange="SetChangeFlag()" style="HEIGHT: 28px; WIDTH: 200px" 
     ></td>
        <td><STRONG><FONT face="MS Sans Serif" size=2>Patient 
      First Name: </FONT></STRONG></td>
        <td><INPUT ID=txtFirstName SIZE=30 MAXLENGTH=80 onchange="SetChangeFlag()" style="HEIGHT: 28px; WIDTH: 200px" 
     ></td>
    </tr>
    <tr>   
        <td><STRONG><FONT face="MS Sans Serif" 
      size=2>Medical&nbsp;Record&nbsp;Number: </FONT></STRONG>   </td>
        <td><INPUT ID=txtMRN SIZE=30 MAXLENGTH=6 onchange="SetChangeFlag()" style="HEIGHT: 28px; WIDTH: 200px" 
     ></td>
        <td><STRONG><FONT face="MS Sans Serif" size=2>X-Ray 
      Number: </FONT></STRONG>  </td>
        <td><INPUT ID=txtXRay SIZE=30 MAXLENGTH=6 onchange="SetChangeFlag()" style="HEIGHT: 28px; WIDTH: 200px" 
     ></td>
    </tr>
    
    <tr>
        <td><STRONG><FONT face="MS Sans Serif" size=2>Date Of 
      Birth: </FONT></STRONG>   </td>
        <td>
      <OBJECT classid=clsid:31E35B1B-4B40-11D3-A8E0-0080C8F9E1FC height=28 
      id=dtDOB style="HEIGHT: 28px; LEFT: 0px; TOP: 0px; WIDTH: 200px" width=200 
      VIEWASTEXT><PARAM NAME="_ExtentX" VALUE="5292"><PARAM NAME="_ExtentY" VALUE="741"><PARAM NAME="BackColor" VALUE="-2147483643"><PARAM NAME="ForeColor" VALUE="-2147483640"><PARAM NAME="Enabled" VALUE="-1"><PARAM NAME="BorderStyle" VALUE="1"><PARAM NAME="Value" VALUE="1/18/00"><PARAM NAME="Appearance" VALUE="1"><PARAM NAME="NullText" VALUE="None"><PARAM NAME="DateFormat" VALUE="mm/dd/yyyy"><PARAM NAME="Alignment" VALUE="0"><PARAM NAME="TodayCaption" VALUE="&amp;Today"><PARAM NAME="NoneCaption" VALUE="&amp;None"><PARAM NAME="AllowNull" VALUE="0"></OBJECT>
      
</td>
        
        <td><STRONG><FONT face="MS Sans Serif" size=2>Date Of 
      Exam:</FONT></STRONG></td>
        <td>
      <OBJECT classid=clsid:31E35B1B-4B40-11D3-A8E0-0080C8F9E1FC height=28 
      id=dtDOE style="HEIGHT: 28px; LEFT: 0px; TOP: 0px; WIDTH: 200px" width=200 
      VIEWASTEXT><PARAM NAME="_ExtentX" VALUE="5292"><PARAM NAME="_ExtentY" VALUE="741"><PARAM NAME="BackColor" VALUE="-2147483643"><PARAM NAME="ForeColor" VALUE="-2147483640"><PARAM NAME="Enabled" VALUE="-1"><PARAM NAME="BorderStyle" VALUE="1"><PARAM NAME="Value" VALUE="1/18/00"><PARAM NAME="Appearance" VALUE="1"><PARAM NAME="NullText" VALUE="None"><PARAM NAME="DateFormat" VALUE="mm/dd/yyyy"><PARAM NAME="Alignment" VALUE="0"><PARAM NAME="TodayCaption" VALUE="&amp;Today"><PARAM NAME="NoneCaption" VALUE="&amp;None"><PARAM NAME="AllowNull" VALUE="0"></OBJECT>
</td>
    </tr>
    
    <tr>
        <td><STRONG><FONT face="MS Sans Serif" size=2>Ordering 
      Physician: </FONT></STRONG>   </td>
        <td>
      <P>
      <OBJECT classid=clsid:8BD21D30-EC42-11CE-9E0D-00AA006002F3 height=28 
      id=cbPhysician style="HEIGHT: 28px; WIDTH: 200px" width=200 VIEWASTEXT><PARAM NAME="VariousPropertyBits" VALUE="746604571"><PARAM NAME="BackColor" VALUE="2147483653"><PARAM NAME="ForeColor" VALUE="2147483656"><PARAM NAME="MaxLength" VALUE="0"><PARAM NAME="BorderStyle" VALUE="0"><PARAM NAME="ScrollBars" VALUE="0"><PARAM NAME="DisplayStyle" VALUE="3"><PARAM NAME="MousePointer" VALUE="0"><PARAM NAME="Size" VALUE="5292;741"><PARAM NAME="PasswordChar" VALUE="0"><PARAM NAME="ListWidth" VALUE="0"><PARAM NAME="BoundColumn" VALUE="1"><PARAM NAME="TextColumn" VALUE="65535"><PARAM NAME="ColumnCount" VALUE="1"><PARAM NAME="ListRows" VALUE="8"><PARAM NAME="cColumnInfo" VALUE="0"><PARAM NAME="MatchEntry" VALUE="1"><PARAM NAME="ListStyle" VALUE="0"><PARAM NAME="ShowDropButtonWhen" VALUE="2"><PARAM NAME="ShowListWhen" VALUE="1"><PARAM NAME="DropButtonStyle" VALUE="1"><PARAM NAME="MultiSelect" VALUE="0"><PARAM NAME="Value" VALUE=""><PARAM NAME="Caption" VALUE=""><PARAM NAME="PicturePosition" VALUE="458753"><PARAM NAME="BorderColor" VALUE="2147483654"><PARAM NAME="SpecialEffect" VALUE="2"><PARAM NAME="Accelerator" VALUE="0"><PARAM NAME="GroupName" VALUE=""><PARAM NAME="FontName" VALUE="Arial Narrow"><PARAM NAME="FontEffects" VALUE="1073741824"><PARAM NAME="FontHeight" VALUE="240"><PARAM NAME="FontOffset" VALUE="0"><PARAM NAME="FontCharSet" VALUE="0"><PARAM NAME="FontPitchAndFamily" VALUE="2"><PARAM NAME="ParagraphAlign" VALUE="1"><PARAM NAME="FontWeight" VALUE="400"></OBJECT>
</P>
        </td>
        
        <td> </td>
        <td><P>&nbsp;</P></td>
    </tr>
    
    <tr>
        <td><STRONG><FONT face="MS Sans Serif" size=2>Patient 
      Status: </FONT></STRONG>  </td>
        <td>
      <P><SELECT id=opStatus style="HEIGHT: 28px; WIDTH: 200px"> 
        <OPTION selected value=0>-- Select Status --</OPTION>
        <OPTION value=1>In-Patient</OPTION>
        <OPTION value=2>Day Surgery</OPTION>
        <OPTION value=3>Emergency Room</OPTION>
        <OPTION value=4>Out-Patient</OPTION>
        <OPTION value=999>Other</OPTION>
        <OPTION value=""></OPTION>
        </SELECT></P></td>
        <td><STRONG><FONT face="MS Sans Serif" size=2>Room 
      Number:</FONT></STRONG></td>
        <td>
      <P>
      <OBJECT classid=clsid:8BD21D30-EC42-11CE-9E0D-00AA006002F3 height=28 
      id=cbRoomNumber style="HEIGHT: 28px; WIDTH: 200px" width=200 VIEWASTEXT><PARAM NAME="VariousPropertyBits" VALUE="746604571"><PARAM NAME="BackColor" VALUE="2147483653"><PARAM NAME="ForeColor" VALUE="2147483656"><PARAM NAME="MaxLength" VALUE="0"><PARAM NAME="BorderStyle" VALUE="0"><PARAM NAME="ScrollBars" VALUE="0"><PARAM NAME="DisplayStyle" VALUE="3"><PARAM NAME="MousePointer" VALUE="0"><PARAM NAME="Size" VALUE="5292;741"><PARAM NAME="PasswordChar" VALUE="0"><PARAM NAME="ListWidth" VALUE="0"><PARAM NAME="BoundColumn" VALUE="1"><PARAM NAME="TextColumn" VALUE="65535"><PARAM NAME="ColumnCount" VALUE="1"><PARAM NAME="ListRows" VALUE="8"><PARAM NAME="cColumnInfo" VALUE="0"><PARAM NAME="MatchEntry" VALUE="1"><PARAM NAME="ListStyle" VALUE="0"><PARAM NAME="ShowDropButtonWhen" VALUE="2"><PARAM NAME="ShowListWhen" VALUE="1"><PARAM NAME="DropButtonStyle" VALUE="1"><PARAM NAME="MultiSelect" VALUE="0"><PARAM NAME="Value" VALUE=""><PARAM NAME="Caption" VALUE=""><PARAM NAME="PicturePosition" VALUE="458753"><PARAM NAME="BorderColor" VALUE="2147483654"><PARAM NAME="SpecialEffect" VALUE="2"><PARAM NAME="Accelerator" VALUE="0"><PARAM NAME="GroupName" VALUE=""><PARAM NAME="FontName" VALUE="Arial Narrow"><PARAM NAME="FontEffects" VALUE="1073741824"><PARAM NAME="FontHeight" VALUE="240"><PARAM NAME="FontOffset" VALUE="0"><PARAM NAME="FontCharSet" VALUE="0"><PARAM NAME="FontPitchAndFamily" VALUE="2"><PARAM NAME="ParagraphAlign" VALUE="1"><PARAM NAME="FontWeight" VALUE="400"></OBJECT></P>
        </td>
    </tr>

    <tr>
        <td><STRONG><FONT face="MS Sans Serif" 
     size=2 
     >Examination:</FONT></STRONG></td>
        <td><INPUT ID=txtExamination SIZE=30 MAXLENGTH=80 onchange="SetChangeFlag()" style="HEIGHT: 28px; WIDTH: 200px" 
     ></td>
        <td><STRONG><FONT face="MS Sans Serif" size=2>Indications:</FONT></STRONG></td>
        <td><INPUT ID=txtIndications SIZE=30 MAXLENGTH=80 onchange="SetChangeFlag()" style="HEIGHT: 28px; WIDTH: 200px" 
     ></td>
    </tr>
    
    <tr>
        <td><STRONG><FONT face="MS Sans Serif" size=2>Notes: 
      </FONT></STRONG> </td>
        <td colspan=3><INPUT id=txtNotes maxLength=80 
      onchange=SetChangeFlag() size=30 style="HEIGHT: 28px; WIDTH: 100%"></td>
    </tr>
    <tr>
        <td colspan=2 ALIGN=right>
      <P>&nbsp;</P></td>
        <td colspan=2 ALIGN=right>
      <P><FONT color=#000000 face="Arial Narrow" size=3><INPUT id=Save onkeydown=OnSave() onmouseup=OnSave() size=35 style="FONT-FAMILY: sans-serif; FONT-SIZE: small; FONT-WEIGHT: bold; HEIGHT: 26px; WIDTH: 94px" type=button value=Save></FONT></P>
      <P>&nbsp;</P></td>
    </tr>
</table>
</form>
<P>
</P>

  <TABLE align=center border=0 cellPadding=3 cellSpacing=1 style="WIDTH: 100%">
    <TBODY>
    <tr>
        <td align=left><font face="Arial Narrow"> &nbsp; </font><STRONG><FONT face="MS Sans Serif" 
      size=2>Report Filename: </FONT></STRONG></td>
        <td align=left colspan=2>
      <P><INPUT name=txtFileName SIZE=50 MAXLENGTH=120 onchange="SetChangeFlag()" style="HEIGHT: 28px; WIDTH: 400px" 
     >
</P></td>
        <td></td>
        <td></td>
    </tr>
    
    </TBODY>
    </TABLE>

<P>
<OBJECT classid=clsid:5C089EAC-D0C1-11D2-9FFF-006097811292 id=DRegEdit1 
style="LEFT: 0px; TOP: 0px" VIEWASTEXT><PARAM NAME="_Version" VALUE="131074"><PARAM NAME="_ExtentX" VALUE="1058"><PARAM NAME="_ExtentY" VALUE="1058"><PARAM NAME="_StockProps" VALUE="0"></OBJECT>

</body>
</html>
